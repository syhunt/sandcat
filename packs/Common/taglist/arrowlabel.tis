class ArrowLabel : Behavior
    {
      function attached() { this.paintBackground = this.paint; }
          
      function getShape()
      {
        var shape = this.shape;
        if(!shape) 
        {
          shape = this.shape = new Graphics.Path();
          this.shapeWidth = null;
          this.shapeHeight = null;
        }
       
        var (x1,y1,x2,y2) = this.box(#rect,#border);
        var w = (x2-x1);
        var h = (y2-y1);
       
        if( this.shapeWidth == w && this.shapeHeight == h)
          return shape; // nothing to do already calculated

        this.shapeWidth = w;
        this.shapeHeight = h;
        
        shape.reset();
        var d = h / 6.0;
        
        x1 += 0.5; y1 += 0.5; x2 += 0.5; y2 += 0.5;
        
        if( this.index == 0 )
          shape.moveTo(x1,y1)
             .lineTo(x2-d,y1)
             .lineTo(x2+d,(y1+y2)/2)
             .lineTo(x2-d,y2)
             .lineTo(x1,y2)
             .close();
        else
          shape.moveTo(x1-d,y1)
             .lineTo(x2-d,y1)
             .lineTo(x2+d,(y1+y2)/2)
             .lineTo(x2-d,y2)
             .lineTo(x1-d,y2)
             .lineTo(x1+d,(y1+y2)/2)
             .close();

        return shape; 
      }      
      
      // function is called while handling WM_PAINT 
      function paint(gfx)
      {
        if(var shape = this.getShape())
        {
          gfx.lineWidth(1)
             .lineColor(color(128,128,128))
             .fillColor(color(this.style#background-color))
             .drawPath( shape );
          return true;
        }
      }
    }